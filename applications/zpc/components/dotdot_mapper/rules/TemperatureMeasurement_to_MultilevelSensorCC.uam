
//Sensor multilevel CC
def zwSENSOR_MULTILEVEL_SENSOR_TYPE  0x3102
def SCALE                            0x3104
def VALUE                            0x3105 // Value is int32 with 3 decimal points, i.e. 1000 means 1

def ZW_SENSOR_TYPE_AIR_TEMPERATURE 0x01

//the DotDot TemperatureMeasurement cluster
def zb_MEASURED_VALUE         0x04020000              // DOTDOT_ATTRIBUTE_ID_TEMPERATURE_MEASUREMENT_MEASURED_VALUE
def zb_MIN_MEASURED_VALUE     0x04020001              // DOTDOT_ATTRIBUTE_ID_TEMPERATURE_MEASUREMENT_MIN_MEASURED_VALUE
def zb_MAX_MEASURED_VALUE     0x04020002              // DOTDOT_ATTRIBUTE_ID_TEMPERATURE_MEASUREMENT_MAX_MEASURED_VALUE
def zb_MEASUREMENT_TOLERANCE  0x04020003              // DOTDOT_ATTRIBUTE_ID_TEMPERATURE_MEASUREMENT_TOLERANCE

scope 0 {

r'zb_MEASURED_VALUE =
  if(r'zwSENSOR_MULTILEVEL_SENSOR_TYPE[ZW_SENSOR_TYPE_AIR_TEMPERATURE].SCALE == 0)
    (r'zwSENSOR_MULTILEVEL_SENSOR_TYPE[ZW_SENSOR_TYPE_AIR_TEMPERATURE].VALUE / 10)
  if(r'zwSENSOR_MULTILEVEL_SENSOR_TYPE[ZW_SENSOR_TYPE_AIR_TEMPERATURE].SCALE == 1)
    (((r'zwSENSOR_MULTILEVEL_SENSOR_TYPE[ZW_SENSOR_TYPE_AIR_TEMPERATURE].VALUE - 32000) * 5) / 90)
    undefined

r'zb_MIN_MEASURED_VALUE =
  if(r'zb_MEASURED_VALUE > r'zb_MIN_MEASURED_VALUE) undefined r'zb_MEASURED_VALUE

r'zb_MAX_MEASURED_VALUE =
  if(r'zb_MEASURED_VALUE < r'zb_MAX_MEASURED_VALUE) undefined r'zb_MEASURED_VALUE

// Let's say the tolerance is 0.5 degrees ? We cannot get this information from Z-Wave devices.
r'zb_MEASUREMENT_TOLERANCE =
  if(e'zb_MEASURED_VALUE) 50 undefined
}
