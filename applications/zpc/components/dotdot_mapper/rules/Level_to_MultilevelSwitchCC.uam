// Multilevel Switch CC <-> Level

// Multilevel Switch CC defines
def zwVERSION   0x2601
def zwSTATE     0x2602
def zwVALUE     0x2603
def zwDURATION  0x2604

// Z-Wave Color switch
def zwCOLOR_STATE              0x3303
def zwCOLOR_COMPONENT_ID       0x3304

// OnOff and Level Cluster
def zbON_OFF_CLUSTER_ON_OFF 0x00060000
def zbLEVEL_CLUSTER_LEVEL   0x00080000
def zbLEVEL_CLUSTER_REMAINING_TIME    0x00080001
def zbLEVEL_CLUSTER_TRANSITION_TIME   0x00080010

// Additional read-only Level cluster properties
def zbLEVEL_CLUSTER_MIN_LEVEL 0x00080002
def zbLEVEL_CLUSTER_MAX_LEVEL 0x00080003

// Constants
def min_level 0
def max_level 99

scope 30 chain_reaction(0) clear_desired(0) {

  // Constants.
  r'zbLEVEL_CLUSTER_MIN_LEVEL = if (e'zwSTATE) min_level undefined
  r'zbLEVEL_CLUSTER_MAX_LEVEL = if (e'zwSTATE) max_level undefined

  // Linking reported attributes zwave -> zigbee
  d'zbLEVEL_CLUSTER_LEVEL =
    if ((d'zbON_OFF_CLUSTER_ON_OFF == 0) & (e'zwSTATE)) min_level
    if (d'zwSTATE.zwVALUE < min_level) min_level
    if (d'zwSTATE.zwVALUE > max_level) max_level
    d'zwSTATE.zwVALUE

  r'zbLEVEL_CLUSTER_TRANSITION_TIME =
    if ( r'zwSTATE.zwDURATION == 0 ) 0
    if ( r'zwSTATE.zwDURATION == 0xFF ) 1
    if ( r'zwSTATE.zwDURATION == 0xFE ) 0
    if ( r'zwSTATE.zwDURATION < 0x80 ) (r'zwSTATE.zwDURATION * 10)
    if ( r'zwSTATE.zwDURATION < 0xEC ) ((r'zwSTATE.zwDURATION-127) * 600)
    if ( r'zwSTATE.zwDURATION > 0xEB ) 65535
    undefined
  d'zbLEVEL_CLUSTER_TRANSITION_TIME =
    if ( d'zbON_OFF_CLUSTER_ON_OFF == 0) 0
    if ( d'zwSTATE.zwDURATION == 0 ) 0
    if ( d'zwSTATE.zwDURATION == 0xFF ) 1
    if ( d'zwSTATE.zwDURATION == 0xFE ) 0
    if ( d'zwSTATE.zwDURATION < 0x80 ) (d'zwSTATE.zwDURATION * 10)
    if ( d'zwSTATE.zwDURATION < 0xEC ) ((d'zwSTATE.zwDURATION-127) * 600)
    if ( d'zwSTATE.zwDURATION > 0xEB ) 65535
    undefined
}

// Clear desired here, so we can keep moving the reported value (transition)
// without getting a reported/desired mismatch
scope 30 chain_reaction(0) clear_desired(1) {

  r'zbLEVEL_CLUSTER_REMAINING_TIME =
    if ( r'zwSTATE.zwDURATION == 0 ) 0
    if ( r'zwSTATE.zwDURATION == 0xFF ) 0
    if ( r'zwSTATE.zwDURATION == 0xFE ) 0
    if ( r'zwSTATE.zwDURATION < 0x80 ) (r'zwSTATE.zwDURATION * 10)
    if ( r'zwSTATE.zwDURATION < 0xEC ) ((r'zwSTATE.zwDURATION-127) * 600)
    if ( r'zwSTATE.zwDURATION > 0xEB ) 65535
    undefined
}

scope 30 chain_reaction(1) clear_desired(0) {

  // Linking desired attributes zigbee -> zwave
  d'zwSTATE.zwVALUE =
    if (d'zbON_OFF_CLUSTER_ON_OFF == 0) min_level
    if (d'zbLEVEL_CLUSTER_LEVEL > r'zbLEVEL_CLUSTER_MAX_LEVEL) r'zbLEVEL_CLUSTER_MAX_LEVEL
    if (d'zbLEVEL_CLUSTER_LEVEL < r'zbLEVEL_CLUSTER_MIN_LEVEL) r'zbLEVEL_CLUSTER_MIN_LEVEL
    d'zbLEVEL_CLUSTER_LEVEL

  d'zwSTATE.zwDURATION =
    if (d'zbON_OFF_CLUSTER_ON_OFF == 0) 0
    if (d'zbLEVEL_CLUSTER_TRANSITION_TIME == 1) 0xFF
    if (d'zbLEVEL_CLUSTER_TRANSITION_TIME < 1270 ) (d'zbLEVEL_CLUSTER_TRANSITION_TIME / 10)
    (127 + d'zbLEVEL_CLUSTER_TRANSITION_TIME / 600)

  // Reported of the level cluster follows the Z-Wave value.
  r'zbLEVEL_CLUSTER_LEVEL =
    if ((r'zbON_OFF_CLUSTER_ON_OFF == 0) & (e'zwSTATE)) min_level
    if (r'zwSTATE.zwVALUE < min_level) min_level
    if (r'zwSTATE.zwVALUE > max_level) max_level
    r'zwSTATE.zwVALUE
}
