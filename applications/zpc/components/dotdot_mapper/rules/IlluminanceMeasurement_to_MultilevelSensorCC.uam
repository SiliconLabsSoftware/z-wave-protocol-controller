
// Multilevel Sensor Command Class
def zwTYPE          0x3102
def zwSCALE         0x3104
def zwVALUE         0x3105 // Value is int32 with 3 decimal points, i.e. 1000 means 1

// We have an Illuminance sensor in Multilevel sensor.
def ZW_SENSOR_TYPE_ILLUMINANCE 0x03
def ZW_PERCENTAGE_SCALE 0
def ZW_LUX_SCALE 1

// ZCL IlluminanceMeasurement cluster attributes
def zb_ILLUMINANCE_MEASUREMENT_MEASURED_VALUE         0x04000000
def zb_ILLUMINANCE_MEASUREMENT_MIN_MEASURED_VALUE     0x04000001
def zb_ILLUMINANCE_MEASUREMENT_MAX_MEASURED_VALUE     0x04000002
def zb_ILLUMINANCE_MEASUREMENT_TOLERANCE              0x04000003
def zb_ILLUMINANCE_MEASUREMENT_LIGHT_SENSOR_TYPE      0x04000004

scope 0 chain_reaction(1) clear_desired(0) {

  /* ZCL spec:
    MeasuredValue represents the Illuminance in Lux (symbol lx) as follows:
    MeasuredValue = 10,000 x log10 Illuminance + 1
    Where 1 lx <= Illuminance <=3.576 Mlx, corresponding to a MeasuredValue in the range 1 to 0xfffe.
  */
  r'zb_ILLUMINANCE_MEASUREMENT_MEASURED_VALUE =
    if(r'zwTYPE[ZW_SENSOR_TYPE_ILLUMINANCE].zwSCALE == ZW_LUX_SCALE) ((10000 * fn_log10(r'zwTYPE[ZW_SENSOR_TYPE_ILLUMINANCE].zwVALUE)) + 1)
      undefined

  r'zb_ILLUMINANCE_MEASUREMENT_MIN_MEASURED_VALUE =
    if(r'zb_ILLUMINANCE_MEASUREMENT_MEASURED_VALUE > r'zb_ILLUMINANCE_MEASUREMENT_MIN_MEASURED_VALUE) undefined r'zb_ILLUMINANCE_MEASUREMENT_MEASURED_VALUE

  r'zb_ILLUMINANCE_MEASUREMENT_MAX_MEASURED_VALUE =
    if(r'zb_ILLUMINANCE_MEASUREMENT_MEASURED_VALUE < r'zb_ILLUMINANCE_MEASUREMENT_MAX_MEASURED_VALUE) undefined r'zb_ILLUMINANCE_MEASUREMENT_MEASURED_VALUE

  // Let's say the tolerance is 5 Lux? We cannot get this information from Z-Wave devices.
  r'zb_ILLUMINANCE_MEASUREMENT_TOLERANCE =
    if(e'zb_ILLUMINANCE_MEASUREMENT_MEASURED_VALUE) 5 undefined

  // Type of light sensor is unknown:
  r'zb_ILLUMINANCE_MEASUREMENT_LIGHT_SENSOR_TYPE = if(e'zb_ILLUMINANCE_MEASUREMENT_MEASURED_VALUE) 0xff undefined
}
