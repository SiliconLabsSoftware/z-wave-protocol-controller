// Multilevel Sensor Command Class
def zwTYPE          0x3102
def zwSCALE         0x3104
def zwVALUE         0x3105 // Value is int32 with 3 decimal points, i.e. 1000 means 1

// Soil Humidity
def SENSOR_TYPE_SOIL_HUMIDITY           0x2A
def SENSOR_SCALE_SOIL_HUMIDITY_PERCENT  0

// ZCL attributes for SoilMoisture (defined in the XML file called WaterContentMeasurement). It will be in the range [0;10000] representing [0;100]%
def zbSOIL_MOISTURE_MEASURED_VALUE        0x04080000
def zbSOIL_MOISTURE_MIN_MEASURED_VALUE    0x04080001
def zbSOIL_MOISTURE_MAX_MEASURED_VALUE    0x04080002
def zbSOIL_MOISTURE_TOLERANCE             0x04080003

scope 0 {

  // ZPC stores (percents * 1000) from Z-Wave, we need to indicate percents*100 in ZCL, so we just divide by 10
  r'zbSOIL_MOISTURE_MEASURED_VALUE =
    if (r'zwTYPE[SENSOR_TYPE_SOIL_HUMIDITY].zwSCALE == SENSOR_SCALE_SOIL_HUMIDITY_PERCENT) (r'zwTYPE[SENSOR_TYPE_SOIL_HUMIDITY].zwVALUE / 10)
    undefined

  r'zbSOIL_MOISTURE_MIN_MEASURED_VALUE =
    if (r'zbSOIL_MOISTURE_MEASURED_VALUE > r'zbSOIL_MOISTURE_MIN_MEASURED_VALUE) undefined r'zbSOIL_MOISTURE_MEASURED_VALUE

  r'zbSOIL_MOISTURE_MAX_MEASURED_VALUE =
    if (r'zbSOIL_MOISTURE_MEASURED_VALUE < r'zbSOIL_MOISTURE_MAX_MEASURED_VALUE) undefined r'zbSOIL_MOISTURE_MEASURED_VALUE

  // Let's say the tolerance is 10 ? Z-Wave devices do not provide this information
  r'zbSOIL_MOISTURE_TOLERANCE =
    if (e'zbSOIL_MOISTURE_MEASURED_VALUE) 10 undefined
}
