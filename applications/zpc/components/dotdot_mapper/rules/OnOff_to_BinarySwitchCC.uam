// Binary switch Command Class
def zwCOMMAND_CLASS_SWITCH_BINARY_VERSION 0x2501
def zwSWITCH_BINARY_STATE 0x2502
def zwSWITCH_BINARY_VALUE 0x2503
def zwSWITCH_BINARY_DURATION 0x2504

// OnOff Cluster
def zbON_OFF 0x00060000

// Capabilities
def zwave_no_binary_switch (e'zwSWITCH_BINARY_STATE == 0)

// Verify if ZCL OnOff Reported/Desired are both defined and mismatched.
def zcl_on_off_mismatch (fn_are_all_defined(r'zbON_OFF,d'zbON_OFF) & (r'zbON_OFF !=d'zbON_OFF))

scope 20 chain_reaction(0) clear_desired(0) {
  // Linking attributes zwave -> zigbee
  r'zbON_OFF =
    if (zwave_no_binary_switch) undefined
    if (fn_are_all_undefined(r'zwSWITCH_BINARY_STATE.zwSWITCH_BINARY_VALUE)) undefined
    if (r'zwSWITCH_BINARY_STATE.zwSWITCH_BINARY_VALUE > 0) 1 0
  d'zbON_OFF =
    if (zwave_no_binary_switch) undefined
    if (zcl_on_off_mismatch) undefined
    if (d'zwSWITCH_BINARY_STATE.zwSWITCH_BINARY_VALUE > 0) 1 0

  // Linking attributes zigbee -> zwave
  d'zwSWITCH_BINARY_STATE.zwSWITCH_BINARY_VALUE =
    if (d'zbON_OFF == 0) 0
    if (d'zbON_OFF == 1) 255
    undefined
}

